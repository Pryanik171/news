def dependencyGroup(Closure closure) {
    closure.delegate = dependencies
    return closure
}


ext {

    // RxKotlin
    depsRxKotlin = dependencyGroup {
        implementation 'io.reactivex.rxjava2:rxkotlin:2.1.0'
        implementation "io.reactivex.rxjava2:rxandroid:2.0.1"
    }

    // Retrofit logging
    depsRetrofitLogging = dependencyGroup {
        debugImplementation 'com.squareup.okhttp3:logging-interceptor:3.8.0'
    }

    // Room without kapt
    depsRoomNoKapt = dependencyGroup {
        implementation "android.arch.persistence.room:runtime:1.1.1"
        implementation "android.arch.persistence.room:rxjava2:1.1.1"
    }

    // Room
    depsRoom = dependencyGroup {
        depsRoomNoKapt()
        kapt "android.arch.persistence.room:compiler:1.1.1"
    }

    //Koin
    depsKoin = dependencyGroup {
        implementation "org.koin:koin-android:$koin_version"
        implementation "org.koin:koin-android-scope:$koin_version"
        implementation "org.koin:koin-android-viewmodel:$koin_version"
        implementation "org.koin:koin-android-ext:$koin_version"
    }

    depsViewModel = dependencyGroup {
        implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_ktx_version"
        implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_ktx_version"
        implementation "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycle_ktx_version"
    }

    // Logger
    depsLogger = dependencyGroup {
        implementation 'ru.digipeople.logger:logger-kotlin:1.0.0'
    }
}